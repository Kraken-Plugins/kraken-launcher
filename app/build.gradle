plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'edu.sc.seis.launch4j' version '3.0.5'
}

def projectVersion = System.getenv("VERSION") ?: '1.0.0'
def runeLiteVersion = 'latest.release'

version = projectVersion
project.version = projectVersion

repositories {
    mavenCentral()
    maven {
        url = "https://repo.runelite.net"
    }
    maven {
        url 'https://jitpack.io'
    }
    mavenLocal()
}

dependencies {
    implementation("com.google.code.gson:gson:2.10.1")
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Required since these need to use system classloader so they can't be included
    // dynamically in the bootstrap.
    implementation("net.bytebuddy:byte-buddy:1.18.4")
    implementation("net.bytebuddy:byte-buddy-agent:1.18.4")

    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation("org.slf4j:slf4j-simple:1.7.36")
    implementation 'ch.qos.logback:logback-classic:1.2.12'

    compileOnly group: 'net.runelite', name:'client', version: runeLiteVersion
    testImplementation 'junit:junit:4.12'
    testImplementation group: 'net.runelite', name:'client', version: runeLiteVersion
    testImplementation group: 'net.runelite', name:'jshell', version: runeLiteVersion
}

processResources {
    inputs.property "version", projectVersion

    filesMatching("kraken-version.properties") {
        expand(version: projectVersion)
    }
}

testing {
    suites {
        test {
            useJUnit('4.13.2')
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

jar {
    archiveBaseName = 'kraken-launcher'
    archiveVersion = projectVersion
}

shadowJar {
    archiveBaseName = 'kraken-launcher'
    archiveVersion = projectVersion
    archiveClassifier = 'fat'

    manifest {
        attributes(
                'Main-Class': 'com.kraken.launcher.Installer',
                'Premain-Class': 'net.bytebuddy.agent.Installer',
                'Agent-Class': 'net.bytebuddy.agent.Installer',
                'Can-Retransform-Classes': 'true',
                'Can-Redefine-Classes': 'true'
        )
    }
}

launch4j {
    mainClassName = 'com.kraken.launcher.Installer'
    // Use the output of shadowJar (the fat jar) instead of the default thin jar
    copyConfigurable = []
    jarTask = tasks.shadowJar
    manifest = "${projectDir}/src/main/resources/app.manifest"
    outfile = 'KrakenInstaller.exe'
    bundledJrePath = 'jre'
    headerType = 'console' // 'console' if you'd like to see whats happening
    stayAlive = true
    errTitle = 'Kraken Launcher Error'
    downloadUrl = 'https://kraken-plugins.com/download'
    supportUrl = "https://kraken-plugins.com/docs/client/download.html"
    companyName = "Kraken Plugins"
    jreMinVersion = '1.11.0'
    icon = "${projectDir}/src/main/resources/favicon.ico"
}
